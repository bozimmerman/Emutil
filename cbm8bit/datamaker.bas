!--------------------------------------------------
!- Saturday, June 03, 2017 2:28:02 PM
!- Import of : 
!- c:\src\emutil\cbm8bit\datamaker.prg
!- Commodore 64
!--------------------------------------------------
5 PK=0:REM set PK to another addy to save to binx.prg
10 P=8192:IFPEEK(P)<>76THENLOAD"emutilasm",8,1
20 L=3000:DIMOF%(200):OX=0:LO=P:OP=P+50:OG=P
30 DIMCA%(20):RESTORE:NC=0
40 READA%:IFA%=0THEN60
50 CA%(NC)=A%:NC=NC+1:GOTO40
60 GOSUB400
70 X=PEEK(P):X1=INT(X/16):X2=X-(X1*16):IFX=0THENY=Y+1
80 IFX<32ORX>36THEN270
90 Z=PEEK(P-2):FL=0:FORI=0TONC:IFZ=CA%(I)ANDP-LO>2THENFL=1
100 NEXTI:IFFL=1OR(P<OPANDZ=X-1)ORP=OG+2THENOF%(OX)=P-LO:LO=P:OX=OX+1
270 PRINTCHR$(65+X1)CHR$(65+X2);:P=P+1:C=C+1:IFC<34THEN70
280 PRINTCHR$(34):IFY>2ANDP>OPTHEN300
290 L=L+10:GOTO60
300 L=L+10:PRINTMID$(STR$(L),2);"dA"+CHR$(34)+CHR$(34)
304 K1=INT(PK/256):K2=PK-(K1*256):LP=OG:L2=LP:DF=INT((PK-OG)/256)
305 IFPK>0THENOPEN8,8,8,"@0:binx,p,w":PRINT#8,CHR$(K2)+CHR$(K1);
310 L=L+10:IFOX=0THENCLOSE8:END
330 GOSUB400:FORI=0TOOX-1
340 X=OF%(I):X1=INT(X/16):X2=X-(X1*16):IFX=0ORX>255THENCLOSE8:STOP
345 IFPK>0THENFORD=L2TOLP+X-1:PRINT#8,CHR$(PEEK(D));:NEXTD
348 IFPK>0THENPX=PEEK(LP+X):PRINT#8,CHR$(PX+DF);:LP=LP+X:L2=LP+1
350 PRINTCHR$(65+X1)CHR$(65+X2);:C=C+1:IFC<34THEN370
360 PRINTCHR$(34):L=L+10:GOSUB400
370 NEXTI:PRINTCHR$(34)
380 IFPK>0THENFORI=L2TOP:PRINT#8,CHR$(PEEK(I));:NEXT:CLOSE8
390 L=L+10:PRINTMID$(STR$(L),2);"dA"+CHR$(34)+CHR$(34):END
400 PRINTMID$(STR$(L),2);"dA"+CHR$(34);:C=0:Y=0:RETURN
500 DATA 174,32,153,173,141,185,140,238,76,172,206,217,205,44,0


